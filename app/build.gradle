plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}


android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "netdesigntool.com.eunions"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 9
        versionName '1.4'
        //testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "netdesigntool.com.eunions.CustomTestRunner"
        multiDexEnabled true
        resConfigs 'en', 'ru'

    }

    signingConfigs {
        release{
            def keyStoreFile = rootProject.file("keystore_add.properties")
            if (! keyStoreFile.exists()) println("No keystore file found for release build variant.")
            else{
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keyStoreFile))
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            //useProguard false
            def suffix

            if ("Linux" == System.getProperty("os.name")) {
                suffix= ".linux.debug"
            } else {
                suffix= ".debug"
            }
            applicationIdSuffix suffix
            println("added sufix: "+ suffix)
        }

        buildFeatures {
            viewBinding true
            compose true
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion="1.2.0-beta03"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lint {
        abortOnError false
    }
    namespace 'netdesigntool.com.eunions'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        //freeCompilerArgs += "-Xopt-in=io.ktor.util.KtorExperimentalAPI"
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-opt-in=org.mylibrary.OptInAnnotation"

        // For resolve error: Compose Compiler requires XXX Kotlin version
        freeCompilerArgs += "-P"
        freeCompilerArgs += "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"
    }
}

//Prevent Error: DuplicatePlatformClasses
configurations {
    all {
        exclude module: 'httpclient'
    }
}



configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.hamcrest:hamcrest-core') using module('org.hamcrest:hamcrest:2.1')
        substitute module('org.hamcrest:hamcrest-library') using module('org.hamcrest:hamcrest:2.1')
    }
}

// For JUnit5 only
//tasks.withType(Test) {
//    useJUnitPlatform()
//}


dependencies {

    implementation project(path: ':data')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation 'androidx.annotation:annotation-experimental:1.3.0'
    implementation 'com.google.ar:core:1.35.0'
    androidTestImplementation project(path: ':app')

    def compose_version = '1.3.2'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"


    def lifeCycleVer = '2.5.1'
    //noinspection KtxExtensionAvailable
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifeCycleVer"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifeCycleVer"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifeCycleVer"

    implementation "androidx.activity:activity-ktx:1.6.1"
    implementation "androidx.fragment:fragment-ktx:1.5.5"


    //              Kotlin coroutines
    def coroutinesVer = '1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVer"


    //              RxJava
    //implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //              AboutActivity
    implementation 'com.github.biagiopietro:AboutActivity:1.4'

    //              MPAndroid charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    //   =============== Jetpack Compose ====================
    implementation 'androidx.compose.compiler:compiler:1.3.2'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.6.1'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.3.1'
    // Animations
    //implementation 'androidx.compose.animation:animation:1.0.5'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.3.2'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation "com.google.accompanist:accompanist-appcompat-theme:0.16.0"
    // UI Tests
    //androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.0.5'


    //          Paging
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"
    implementation "androidx.paging:paging-compose:1.0.0-alpha17"

    //          Hilt
    def hilt_ver = '2.44.2'
    implementation "com.google.dagger:hilt-android:$hilt_ver"
    kapt "com.google.dagger:hilt-compiler:$hilt_ver"

    //          Room
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"


    //   Test
    def mockitoInlVer = "4.10.0"
    def mockitoKotlinVer = "4.1.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation "org.mockito:mockito-core:+"
    testImplementation "org.mockito:mockito-inline:$mockitoInlVer"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVer"
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVer"

    
    //    AndroidTest
    androidTestImplementation "androidx.test:runner:1.5.1"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation "org.mockito:mockito-android:+"
    //androidTestImplementation "org.mockito:mockito-inline:+"
    //androidTestImplementation "org.mockito.kotlin:mockito-kotlin:+"
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'org.awaitility:awaitility:4.2.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.3.2"

    debugImplementation "androidx.fragment:fragment-testing:1.6.0-alpha04"

    // *** Hilt ***
    // For instrumentation tests
    androidTestImplementation  "com.google.dagger:hilt-android-testing:${hilt_ver}"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:${hilt_ver}"
    androidTestAnnotationProcessor "com.google.dagger:hilt-compiler:${hilt_ver}"

    // For local unit tests
    /*testImplementation "com.google.dagger:hilt-android-testing:${hilt_ver}"
    testAnnotationProcessor "com.google.dagger:hilt-compiler:${hilt_ver}"*/

}

//  For using Hilt with Kotlin
kapt {
    correctErrorTypes true
}